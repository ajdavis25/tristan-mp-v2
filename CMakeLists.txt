cmake_minimum_required(VERSION 3.16)

project(tristan-v2 VERSION 2.9
        DESCRIPTION "Relativistic cartesian particle-in-cell code"
        LANGUAGES C CXX Fortran)

set(CMAKE_Fortran_MODULE_DIRECTORY ${CMAKE_BINARY_DIR}/fortran_modules)

if (DEFINED ENV{HDF5_ROOT})
  message(NOTICE "HDF5 found")
  set(HDF5_ROOT $ENV{HDF5_ROOT})
endif()
if (DEFINED ENV{I_MPI_ROOT})
  message(NOTICE "Intel MPI found")
  set(I_MPI_ROOT $ENV{I_MPI_ROOT})
endif()

option(hdf5 "Use HDF5" OFF)
option(intel "Use Intel compiler" ON)

set(DEBUG "OFF" CACHE BOOL "Debug mode")

add_compile_definitions(STR_MAX=280)
add_compile_definitions(TINYXYZ=1e-6)
add_compile_definitions(TINYREAL=1e-3)
add_compile_definitions(TINYFLD=1e-8)
add_compile_definitions(TINYWEI=1e-6)
add_compile_definitions(M_PI=3.141592653589793)
add_compile_definitions(VEC_LEN=16)

set(vectorization "None" CACHE STRING "Vectorization level (None, avx2, avx512)")

set(CMAKE_BUILD_TYPE "Release" CACHE STRING "Build type")
add_compile_definitions(NDEBUG)

if (${vectorization} STREQUAL "avx2")
    set(CMAKE_C_FLAGS   "${CMAKE_C_FLAGS} -mavx2")
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -mavx2")
    set(CMAKE_Fortran_FLAGS "${CMAKE_Fortran_FLAGS} -mavx2")
elseif (${vectorization} STREQUAL "avx512")
    set(CMAKE_C_FLAGS   "${CMAKE_C_FLAGS} -mavx512f")
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -mavx512f")
    set(CMAKE_Fortran_FLAGS "${CMAKE_Fortran_FLAGS} -mavx512f")
elseif (${vectorization} STREQUAL "None")
    # Do nothing
else()
    message(FATAL_ERROR "Vectorization level not supported")
endif()

# -- options --
# architecture
option(mpi08 "Use MPI08" ON)
option(mpinonblock "Use non-blocking MPI" OFF)
option(lowmem "Use low memory mode" OFF)
option(ifport "Use ifport" OFF)
# optimizations
option(dwn "Use particle downsampling" OFF)
option(slb "Use static load balancing" OFF)
option(alb "Use adaptive load balancing" OFF)
option(payload "Use particle payloads" OFF)
# output
option(serialoutput "Output in serial" OFF)
option(usroutput "Enable custom user data outputting" OFF)
# physics
option(extfields "Use external fields" OFF)
option(absorb "Use absorbing boundary conditions" OFF)
option(vay "Use Vay current deposition" OFF)
option(blinne "Use Blinne field solvers" OFF)
option(emit "Enable photon emission" OFF)

set(precision "single" CACHE STRING "Precision of the code (single or double)")
set(nghosts "2" CACHE STRING "Number of ghost cells (default 2)")
set(radiation "OFF" CACHE STRING "Radiation drag (or OFF)")

# -- additional configs for each option --
if (${mpi08} STREQUAL "ON")
  add_compile_definitions(MPI08)
else()
  add_compile_definitions(MPI)
endif()
if (${mpinonblock} STREQUAL "ON")
  add_compile_definitions(MPINONBLOCK)
endif()
if (${lowmem} STREQUAL "ON")
  add_compile_definitions(LOWMEM)
endif()
if (${ifport} STREQUAL "ON")
  add_compile_definitions(IFPORT)
endif()
if (${dwn} STREQUAL "ON")
  add_compile_definitions(DOWNSAMPLING)
endif()
if (${slb} STREQUAL "ON")
  add_compile_definitions(SLB)
endif()
if (${alb} STREQUAL "ON")
  add_compile_definitions(ALB)
endif()
if (${payload} STREQUAL "ON")
  add_compile_definitions(PRTLPAYLOADS)
endif()
if (${serialoutput} STREQUAL "ON")
  add_compile_definitions(SERIALOUTPUT)
endif()
if (${usroutput} STREQUAL "ON")
  add_compile_definitions(USROUTPUT)
endif()
if (${extfields} STREQUAL "ON")
  add_compile_definitions(EXTERNALFIELDS)
endif()
if (${absorb} STREQUAL "ON")
  add_compile_definitions(ABSORB)
endif()
if (${vay} STREQUAL "ON")
  add_compile_definitions(VAY)
endif()
if (${blinne} STREQUAL "ON")
  add_compile_definitions(BLINNE)
endif()
if (${emit} STREQUAL "ON")
  add_compile_definitions(EMIT)
endif()

if (${precision} STREQUAL "single")
  add_compile_definitions(default_h5_real=H5T_NATIVE_REAL)
else()
  add_compile_definitions(DPREC)
  add_compile_definitions(default_h5_real=H5T_NATIVE_DOUBLE)
  if (CMAKE_Fortran_COMPILER_ID MATCHES "GNU")
    set(CMAKE_Fortran_FLAGS "${CMAKE_Fortran_FLAGS} -fdefault-real-8")
  elseif (CMAKE_Fortran_COMPILER_ID MATCHES "Intel")
    set(CMAKE_Fortran_FLAGS "${CMAKE_Fortran_FLAGS} -r8")
  endif()
endif()

add_compile_definitions(NGHOST=${nghosts})

if (${radiation} MATCHES "sync")
  add_compile_definitions(SYNCHROTRON)
  add_compile_definitions(RADIATION)
endif()
if (${radiation} MATCHES "ic")
  add_compile_definitions(INVERSECOMPTON)
  add_compile_definitions(RADIATION)
endif()

# select the dimension
set (dim "3" CACHE STRING "dimension")
if (dim STREQUAL "1")
  add_compile_definitions(oneD)
elseif (dim STREQUAL "2")
  add_compile_definitions(twoD)
elseif (dim STREQUAL "3")
  add_compile_definitions(threeD)
else()
  message(FATAL_ERROR "Please specify dimension (1,2,3) with `-D dim=<DIM>`")
endif()

# user/unit selection is required for the Fortran build
set(user "" CACHE STRING "userfile")
set(unit "" CACHE STRING "unitfile")
set(userfile "")
if (NOT user STREQUAL "" AND NOT unit STREQUAL "")
  message(FATAL_ERROR "Please specify only one of user or unit")
elseif (NOT user STREQUAL "")
  set(userfile "${CMAKE_CURRENT_SOURCE_DIR}/user/${user}.F90")
  if (NOT EXISTS ${userfile})
    message(FATAL_ERROR "User file '${userfile}' not found")
  endif()
elseif (NOT unit STREQUAL "")
  set(userfile "${CMAKE_CURRENT_SOURCE_DIR}/unit/${unit}.F90")
  if (NOT EXISTS ${userfile})
    message(FATAL_ERROR "Unit file '${userfile}' not found")
  endif()
else()
  file(GLOB userfiles ${CMAKE_CURRENT_SOURCE_DIR}/user/*.F90)
  file(GLOB unitfiles ${CMAKE_CURRENT_SOURCE_DIR}/unit/*.F90)
  message(STATUS "Please specify userfile or unitfile with `-D user=<USERFILE>` or `-D unit=<UNITFILE>`:")
  message(STATUS "userfiles:")
  foreach(userfileCandidate ${userfiles})
    get_filename_component(userfileCandidate ${userfileCandidate} NAME)
    string(REGEX REPLACE ".F90$" "" userfileCandidate ${userfileCandidate})
    message(STATUS ": ${userfileCandidate}")
  endforeach()
  message(STATUS "unitfiles:")
  foreach(unitfileCandidate ${unitfiles})
    get_filename_component(unitfileCandidate ${unitfileCandidate} NAME)
    string(REGEX REPLACE ".F90$" "" unitfileCandidate ${unitfileCandidate})
    message(STATUS ": ${unitfileCandidate}")
  endforeach()
  message(FATAL_ERROR "userfile or unitfile not defined")
endif()

# MPI/HDF5 detection (Fortran required)
find_package(MPI COMPONENTS Fortran C CXX)
if (NOT MPI_FOUND)
  message(FATAL_ERROR "MPI with Fortran support is required. Set CMAKE_Fortran_COMPILER to your MPI wrapper (e.g., mpif90).")
endif()
if (NOT MPI_Fortran_FOUND)
  message(FATAL_ERROR "MPI Fortran component not found. Set CMAKE_Fortran_COMPILER to your MPI wrapper (e.g., mpif90) or install MPI with Fortran bindings.")
endif()

if (${hdf5} STREQUAL "ON")
  find_package(HDF5 COMPONENTS Fortran)
  if (NOT HDF5_FOUND OR NOT HDF5_Fortran_FOUND)
    message(FATAL_ERROR "HDF5 Fortran components requested but not found. Set HDF5_ROOT or disable with -Dhdf5=OFF.")
  endif()
  add_compile_definitions(HDF5)
endif()

add_subdirectory(src)

# -- report --
get_directory_property(Defs COMPILE_DEFINITIONS)
string(REPLACE ";" " " Defs "${Defs}")

message(NOTICE "==========================================================")
message(NOTICE "Tristan v2 has been configured with the following options:")
message(NOTICE "")
message(NOTICE "SETUP:")
message(NOTICE "  Userfile: ${userfile}")
message(NOTICE "  Dim: ${dim}D")
message(NOTICE "  ghost zones: ${nghosts}")
message(NOTICE "  Load balancing: SLB=${slb} ALB=${alb}")
message(NOTICE "  Particle downsampling: ${dwn}")
message(NOTICE "  Particle pusher: Vay=${vay}")
message(NOTICE "  Particle payloads: ${payload}")
message(NOTICE "")
message(NOTICE "PHYSICS:")
message(NOTICE "  External fields: ${extfields}")
message(NOTICE "  Absorbing boundaries: ${absorb}")
message(NOTICE "  Cooling: ${radiation}")
message(NOTICE "  Photon emission: ${emit}")
message(NOTICE "")
message(NOTICE "TECHNICAL:")
message(NOTICE "  Float precision: ${precision}")
message(NOTICE "  Debug mode: ${DEBUG}")
message(NOTICE "  Low memory mode: ${lowmem}")
message(NOTICE "  Output: HDF5=${hdf5} (serial=${serialoutput})")
message(NOTICE "  User output: ${usroutput}")
message(NOTICE "  MPI version: MPI08=${mpi08}")
message(NOTICE "  IFPORT mkdir: ${ifport}")
message(NOTICE "..........................................................")
message(NOTICE "C compiler: ${CMAKE_C_COMPILER} ${CMAKE_C_FLAGS}")
message(NOTICE "CXX compiler: ${CMAKE_CXX_COMPILER} ${CMAKE_CXX_FLAGS}")
message(NOTICE "Fortran compiler: ${CMAKE_Fortran_COMPILER} ${CMAKE_Fortran_FLAGS}")
message(NOTICE "Definitions: ${Defs}")
message(NOTICE "==========================================================")
